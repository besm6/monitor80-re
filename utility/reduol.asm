шифр 419900зс5^
dis 67(80-wr)^
еев1а3
*name
*no list
*assem
 REDUOL:,NAME,DTRAN  /01.06.84/
 INFSUB:,EQU,2.2B
 ADR:,EQU,INFSUB
 TYPE:,EQU,3.3B
 INSTR:,EQU,TYPE
 ADR1:,EQU,TYPE
 STR:,EQU,4.4B
 ADR2:,EQU,STR
 STR1:,EQU,5.5B
 ADR3:,EQU,STR1
 STR2:,EQU,6.6B
 ADR4:,EQU,STR2
 NBODY:,EQU,7.7B
 MENDATA:,EQU,NBODY
 AC:,EQU,8.10B
 AS:,EQU,9.11B
 AU:,EQU,10.12B
 MODE:,EQU,11.13B
 REGIM:,EQU,12.14B
 SECENTRY:,EQU,13.15B
 ALF1:,EQU,14.16B
 ALF2:,EQU,15.17B
 OVFLJUMP:,EQU,16.20B
 IR1:,EQU,17.21B
 IR12:,EQU,28.34B
 IR13:,EQU,29.35B
 NSET:,EQU,31.37B
 NDATA:,EQU,32.40B
 MENDSUB:,EQU,NDATA
 R:,EQU,33.41B
 DEFADR:,EQU,34.42B
 LEX:,EQU,35.43B
 ALFA:,EQU,36.44B
 RLEX:,EQU,37.45B
 RCAT:,EQU,39.47B
 RETN:,EQU,41.51B
 AEX:,EQU,43.53B
 CARWORD:,EQU,46.56B
 AR:,EQU,47.57B
 QNAME:,EQU,62B
 RELADR:,EQU,51.63B
 ERROFLAG:,EQU,52.64B
 LOCSUBP:,EQU,53.65B
 WORDADR:,EQU,54.66B
 ADATA:,EQU,56.70B
 ASUBP:,EQU,57.71B
 SHIFR:,EQU,58.72B
 LTAPES:,EQU,59.73B
 LOADLIBR:,EQU,74B.60
 ERN:,EQU,116B
 ER:,EQU,117B
 RER:,EQU,120B
 CSMSK:,EQU,121B
 WREX:,EQU,122B
 SAVE20:,EQU,124B
 SECLIST:,EQU,125B
 GLOBASE:,EQU,126B
 MEMERFLG:,EQU,127B
 AQUEUE:,EQU,130B
 LISTEND:,EQU,131B
 NFRIEDR:,EQU,132B
 CATDRUM:,EQU,133B
 ENTRY:,EQU,134B
 LIBCODE:,EQU,136B
 CATEND:,EQU,137B
 LISTRETN:,EQU,141B
 LB:,EQU,142B
 LE:,EQU,143B
 AQ:,EQU,144B
 LIBSHIFT:,EQU,145B
 IPRT:,EQU,146B
 RWR:,EQU,147B
 FRIEDR:,EQU,150B
 FRIEPAGE:,EQU,151B
 S2:,EQU,152B
 S1:,EQU,153B
 GIVPLACE:,EQU,154B
 NPRT:,EQU,156B
 TPRT:,EQU,157B
 GIVPLAC1:,EQU,GIVPLACE+1
 ABSFLG:,EQU,220B
 DPRINT:,EQU,221B
 BUFP:,EQU,222B
 BUF:,EQU,230B
 TYESFLG:,EQU,254B
 TCOR:,EQU,255B
 TCORV:,EQU,256B
 TCORLIB:,EQU,323B
 TCOREND:,EQU,365B
 READFIXS:,EQU,ERROFIX
 *C:13,REL,*C
c entry 0000
 14,VTM,
 ,WTC,LEX.
с entry 0001
 :14,VZM,MNENT-*C
 ,UJ,entry3
с entry 0002
 :,WTC,LEX.
 ,UJ,SPENT-*C
 entry3:,WTC,LEX.
 13,VTM,
 ,ATX,TYESFLG.
 ,DRUM,RPAGE1.
 ,WTC,MODE
 14,VTM,
 14,XTA,
 14,XTS,1
 ,ATX,ALF1
 14,XTA,2
 ,ARX,C8.
 ,ATX,ALF2
 ,UJ,LOAD
 SPENT:,WTC,ALF2
 14,VTM,-10B
 14,ATX,
 ,XTA,ALF1
 14,ATX,1
 14,XTA,
 MNENT:15,ATX,
 ,RTE,177B
 ,ATX,REGIM
 ,ITA,14
 ,ATX,MODE
 ,ITA,13
 ,ATX,IR13
 ,WTC,LEX.
 13,VTM,
 ,NTR,3
 14,VTM,1-12
 *1:14,ITA,12
 14,ATX,IR12
 14,VLM,*1
 ,XTA,
 ,ATX,TYESFLG.
 LOAD:3,VTM,ERN-247B
 ,XTA,
 :3,ATX,247B
  3,VLM,*
 ,XTA,NORMJUMP
 ,CTX,TCOR
 3,VTM,256B-314B
 ,XTA,PRTJUMP
 :3,CTX,314B
  3,VLM,*-1
 ,XTA,EXTJUMP
 3,VTM,315B-TCOREND
 :3,CTX,TCOREND
  3,VLM,*-1
 ,XTA,
 ,ATX,ERROFLAG
 ,ATX,MEMERFLG
 ,XTA,OVFLJUMP
 ,ATX,SAVE20
 ,XTA,ERJUMP
 ,NTR,3
 ,CTX,OVFLJUMP
 ,XTA,ALF1
 ,ASN,64-3
 11,VTM,-2
 ,CTX,
 STORDIST:,ASN,64-15
 15,ATX,
 ,YTA,
 11,STX,AU.
 11,VLM,STORDIST
 ,XTA,ALF2
 ,ASN,64+30
 ,ARX,BEGLIST.
 ,ATX,LISTEND
 ,ATX,AQUEUE
 ,ATX,SECLIST.
 ,XTA,ALF2
 ,AAX,M3L.
 ,ATX,ALF2
 ,XTA,AU.
 ,ATI,2
 ,XTA,ALF2
 ,UJ,NZERO
 CLAU:,XTA,AU.
 ,ATI,2
 ,AAX,MP.
 NZERO:,A-X,AU.
 ,ATI,4
 14,VTM,INITDRUM
 ,X-A,
 ,AAX,MP.
 ,UZA,ZEROSET
 ,XTA,ALF2
 ,AAX,MP.
 ,ARX,C1777.
 ,ATI,2
 ,X-A,ALF2
 ,ATI,4
 14,VTM,CLAU
 ,UJ,ZEROSET
 ZEROS:14,VTM,SKIPERAS
 ZEROS1:,XTA,FRIEPAGE
 ,ASN,64+24
 ZEROS2:,ATI,2
 2,UTM,1023
 4,VTM,-1023
 ZEROSET:,ITA,
 4,UTC,
 2,ATX,
 4,VLM,ZEROSET
 14,UJ,
 INITDRUM:,XTA,ALFA.
 ,ASN,64+25
 ,AAX,M37.
 ,ARX,CM1.
 ,ATX,NPRT
 ,XTA,CNFRIEDR.
 ,ATX,NFRIEDR
 ,XTA,CGIVPLAC
 ,CTX,GIVPLACE
 ,XTA,CGIVPLC1
 ,CTX,GIVPLAC1
 ,XTA,RCAT.
 ,APX,MPDRUM
 ,ASN,64+36
 ,ATX,CATDRUM
 4,VTM,
 ,XTA,ALF2
 ,AAX,M3L.
 ,ASN,64+10
 ,U1A,*8
 ,XTA,FRIMEMJ
 ,CTX,TCORV
 ,XTA,C1.
 *8:,ATI,1
 ,ATI,2
 ,X-A,NPRT
 ,UZA,UPPLOOP
 ,XTA,NPRT
 ,ATI,1
 ,ATI,2
 UPPLOOP:2,UTM,1
 ,ITA,2
 ,X-A,NPRT
 ,U1A,LOWLOOP
 ,XTA,FRIMEMJ
 2,CTX,TCOR
 ,ITA,2
 ,ASN,64-34
 ,ITS,2
 15,AOX,
 4,ATX,TPRT
 4,UTM,1
 ,UJ,UPPLOOP
 LOWLOOP:,ITA,1
 LOWLOOP1:,ARX,CM1.
 ,UZA,CLP
 ,ATI,2
 ,ASN,64-34
 ,ITS,2
 15,AOX,
 4,ATX,TPRT
 4,UTM,1
 ,ITA,2
 ,UJ,LOWLOOP1
 CLP:,ITS,1
 ,ASN,64-34
 ,ATX,FRIEPAGE
 ,ASN,64+24
 ,ATI,5
 ,XTS,
 ,XTS,
 6,VJM,SKIPERAS
 ,XTA,ADRCAT.
 ,ATX,CATEND
 SCHCEND:,ARX,C1.
 14,VJM,READWORD.
 ,AEX,CAL7.
 ,UZA,BEGLOAD.
 ,XTA,CATEND.
 ,A+X,C2.
 ,ATX,CATEND
 ,UJ,SCHCEND
 CHEKPAGE:,ITA,14
 ,ITS,2
 ,ITS,4
 ,ITS,1
 ,A-X,NDEFPLAC.
 ,UZA,READPAGE
c CLEANPAG:
 ,ITA,1
 ,ASN,64-34
 ,AEX,FRIEPAGE
 ,UZA,READPAGE
 4,VTM,
 CLOOP:4,XTA,TPRT
 ,AAX,CMEM.
 ,ASN,64+34
 ,ITS,1
 15,AEX,
 ,UZA,SEND
 4,UTM,1
 ,UJ,CLOOP
 SEND:,XTA,FRIEPAGE
 ,ASN,64+24
 ,ATI,2
 4,XTA,TPRT
 ,AAX,LOWMSK.
 ,ATX,S1
 ,AOX,FRIEPAGE
 4,ATX,TPRT
 ,ATI,14
 ,ITA,14
 ,A-X,NLIBR.
 ,U1A,*7
 ,ITA,14
 ,A-X,LIBSHIFT
 ,U1A,CHGFRP
 ,A+X,NLIBR.
 ,ATI,14
 ,X-A,NLIBRMAX.
 ,U1A,CHGFRP
 *7:,ITA,14
 ,ASN,64-34
 ,AAX,CMEM.
 ,X-A,FRIEPAGE
 ,AAX,CMEM.
 14,XTS,TCOR
 ,AAX,MEMCLEAN.
 15,AOX,
 14,CTX,TCOR
 CHGFRP:,ITA,1
 ,ASN,64-34
 ,ATX,FRIEPAGE
 ,ASN,64+24
 ,ATI,4
 14,VTM,-1023
 RESELL:4,XTA,
 2,ATX,
 4,UTM,1
 2,UTM,1
 14,VLM,RESELL
 ,UJ,READPAGE
 READPAGE:,ITA,1
 ,ATX,RWR
 14,VJM,SETFDR
 ,ITA,1
 ,ASN,64-34
 ,AAX,CMEM.
 ,X-A,FRIEPAGE
 ,AAX,CMEM.
 ,AOX,PRTJUMP
 1,CTX,TCOR
 ,XTA,FRIEDR
 ,UZA,ZEROS
 14,VJM,READDRUM
 ,ITA,1
 ,A-X,NLIST.
 ,UZA,SKIPERAS
 ,XTA,FRIEDR
 ,WTC,NFRIEDR
 ,ATX,TFRIEDR
 ,XTA,NFRIEDR
 ,ARX,C1.
 ,ATX,NFRIEDR
 SKIPERAS:,WTC,NPRT
 14,VTM,-2
 ,XTA,NFRIEDR
 ,A-X,C2.
 ,UZA,CLEANP
 SERCHC:14,XTA,TPRT
 ,AAX,LOWMSK.
 ,A-X,NLIST.
 14,UTM,-1
 ,U1A,SERCHC
 14,UTM,1
 CLEANP:,XTA,FRIEPAGE
 14,XTS,TPRT
 ,AAX,CMEM.
 ,ATX,FRIEPAGE
 14,XTA,TPRT
 ,AAX,LOWMSK.
 ,ATX,RWR
 ,ATI,2
 ,ITA,1
 ,A-X,NLIBR.
 ,U1A,*5
 ,A+X,LIBSHIFT
 ,UJ,*6
 *5:,ITA,1
 *6:15,AOX,
 14,ATX,TPRT
 ,XTA,
 ,ATX,FRIEDR
 ,XTA,RWR
 ,A-X,NLIBR.
 ,U1A,CHEKCAT
 ,XTA,RWR
 ,A-X,LIBSHIFT
 ,U1A,ERASPRT
 ,A+X,NLIBR.
 ,ATI,2
 ,X-A,NLIBRMAX.
 ,U1A,ERASPRT
 ,UJ,SETDRUM
 CHEKCAT:,XTA,RWR
 ,A-X,NCAT.
 ,UZA,SETDRUM
 ,WTC,RWR
 ,XTA,TCOR
 ,AEX,FRIMEMJ
 ,UZA,SETDRUM
 CHEKLST:,XTA,RWR.
 ,A-X,NLIST.
 ,U1A,SCHFRDR.
 ,A+X,LSTDRUM.
 ,ATX,FRIEDR.
 ,UJ,WRITDR.LIST DRUM IS FIXED
 SCHFRDR:,XTA,NFRIEDR
 ,A-X,C1.
 ,ATX,NFRIEDR
 ,ATI,14
 ,UTC,-1
 14,XTA,TFRIEDR
 ,ATX,FRIEDR
 14,XTA,TFRIEDR
 ,UTC,-1
 14,ATX,TFRIEDR
 WRITDR:14,VJM,WRITDRUM
 SETDRUM:,XTA,FRIEDR
 ,AOX,EXTJUMP
 2,CTX,TCOR
 ERASPRT:2,VTM,TCORV-TCOREND
 ERASLP:2,XTA,TCOREND
 ,AAX,CPRT.D23
 ,UZA,ERLPEND
 2,XTA,TCOREND
 ,AAX,UPPERMSK.
 ,AOX,PRTJUMP
 2,CTX,TCOREND
 ERLPEND:2,VLM,ERASLP
 ,XTA,
 ,ATX,IPRT
 ,XTA,NORMJUMP
 ,WTC,NPRT
 ,CTX,TCORV
 ,STX,
 ,STI,4
 ,STI,2
 ,ATI,14
 1,UJ,TCOR
 SETPRT:,ITA,14
 ,ITS,1
 ,ATX,S2
 ,A-X,NLIBR.
 ,U1A,*4
 ,A+X,LIBSHIFT
 ,ATX,S2
 *4:,ITA,5
 ,ASN,64+10
 ,ASN,64-34
 ,AOX,S2
 ,ATX,S2
 ,ATX,S1
 ,XTA,IPRT
 ,ATI,14
 ,A+X,C1.
 ,ATX,IPRT
 LSET:14,XTA,TPRT
 ,XTS,S2
 14,STX,TPRT
 ,ATX,S2
 ,AEX,S1
 14,UTM,1
 ,U1A,LSET
 1,XTA,TCOR
 ,AAX,UPPERMSK.
 ,AOX,NORMJUMP
 1,CTX,TCOR
 ,STX,
 ,ATI,14
 6,UJ,
 SETFDR:,ITA,1
 ,A-X,NLIBR.
 ,U1A,CHCAT
 ,A+X,LIBSHIFT
 ,UJ,STFEX
 CHCAT:,ITA,1
 ,A-X,NCAT.
 ,U1A,CHLST
 ,A+X,CATDRUM
 ,UJ,STFEX
 CHLST:,ITA,1
 ,A-X,NLIST.
 ,U1A,CHWAR
 ,A+X,LSTDRUM.
 ,UJ,STFEX
 CHWAR:1,XTA,TCOR
 ,AAX,LOWMSK.
 STFEX:,ATX,FRIEDR
 14,UJ,
 READDRUM:,ITS,14
 ,ITS,
 14,VJM,FA
 ,A+X,CREAD.
 ,ATX,AEX
 ,DRUM,AEX
 ,STX,
 ,STI,14
 14,UJ,
 WRITDRUM:,ITS,14
 ,ITS,
 14,VJM,FA
 ,DRUM,AEX
 ,STX,
 ,STI,14
 14,UJ,
 FA:,XTA,FRIEDR
 ,ASN,64-36
 ,AUX,CUNPDRUM
 ,ATX,AEX
 ,XTA,FRIEDR
 ,AAX,C33777.
 ,A-X,BTAPE.
 ,U1A,SKTP
 ,XTA,FRIEDR
 ,A+X,CTAPE.
 ,ASN,64-24
 ,AUX,CUNPTAPE.
 ,ATX,AEX
 SKTP:,XTA,FRIEPAGE
 ,ASN,64+4
 ,AOX,AEX
 ,ATX,AEX
 14,UJ,
 FRIMEM:,ITA,14
 ,ITS,2
 ,ITS,4
 ,XTS,PRTJUMP
 1,CTX,TCOR
 ,ITA,1
 ,ASN,64-10
 14,VTM,ERASPRT
 ,UJ,ZEROS2
 TFRIEDR:,LOG,1037
 ,LOG,1036
 ,LOG,1035
 ,LOG,1034
 ,LOG,1033
 ,LOG,1032
 ,LOG,1031
 ,LOG,1030
 ,LOG,1027
 ,LOG,1026
 ,LOG,1025
 ,LOG,1024
 ,LOG,1023
 ,LOG,1022
 ,LOG,1021
 ,LOG,1020
 WRITWP1:,XTS,WORDADR
 15,ATX,
 ,ARX,C1.
 ,STX,WORDADR
 WRITWORD:14,MTJ,7
 6,VJM,GIVPLACE
 5,STX,
 5,CTX,
 ,STX,
 7,UJ,
 READWP1:,XTS,RELADR
 15,ATX,
 ,ARX,C1.
 ,STX,RELADR
 READWORD:6,VJM,GIVPLACE
 5,XTA,
 14,UJ,
 BEGLOAD:,STX,
 ,ATX,QNAME
 ,XTA,TSUBP.
 ,ATX,TYPE
 ,ATX,CARWORD
 ,XTA,
 ,XTS,
 14,VJM,INCRAP
 14,VJM,CHEKLIST
 ,XTA,ENTRY
 ,ATX,SECENTRY
 LOADQUE:,XTA,
 ,ATX,GLOBASE
 CHEKQUE:,XTA,AQUEUE
 ,AEX,LISTEND
 ,UZA,ENDLOAD
 ,XTA,AQUEUE
 14,VJM,READWORD
 ,ATX,QNAME
 5,XTA,1
 ,ATX,LIBCODE
 ,AAX,M3L.
 ,ATX,LOCSUBP
 ,XTA,AQUEUE
 ,ARX,C2.
 ,ATX,AQUEUE
 5,XTA,1
 ,AAX,LIBMASK.
 ,UZA,CHEKQUE
 5,XTA,1
 ,AAX,CFIX.
 ,U1A,READFIXS
 ,XTA,LIBCODE
 ,AAX,MSEG.
 ,ASN,64+36
 ,ARX,LIBADR.
 ,ATX,RELADR
 ,XTA,LIBCODE
 ,APX,MPACKSA.
 ,ASN,64+36
 ,ATX,LIBSHIFT
 4,VTM,TCORLIB-TCOREND
 RELLOOP:4,XTA,TCOREND
 ,AAX,CEXT.
 ,U1A,SKEXTSET
 ,XTA,EXTJUMP
 4,CTX,TCOREND
 SKEXTSET:4,VLM,RELLOOP
 ,WTC,NPRT
 4,VTM,
 DECLOOP:4,UTM,-1
 4,XTA,TPRT
 ,AAX,LOWMSK.
 ,A-X,LIBSHIFT
 ,U1A,DECLOOPE
 ,A+X,NLIBR.
 ,ATI,2
 ,X-A,NLIBRMAX.
 ,U1A,DECLOOPE
 4,XTA,TPRT
 ,AAX,CMEM.
 ,ITS,2
 ,ASN,64-34
 ,AAX,CMEM.
 15,X-A,
 ,AAX,CMEM.
 ,ITS,4
 ,A-X,IPRT
 ,UZA,ZPRT
 ,XTA,NORMJUMP
 15,AOX,
 ,UJ,SETJUMP
 ZPRT:,XTA,PRTJUMP
 15,AOX,
 SETJUMP:2,CTX,TCOR
 DECLOOPE:4,V1M,DECLOOP
 10,VTM,-2
 READSTR:14,VJM,READWP1
 10,STX,STR+2
 10,VLM,READSTR
 ,ITA,
 ,ATX,NBODY
 ,XTA,STR+2
 ,ASN,64+30
 ,ARX,STR+2
 ,AAX,M3L.
 ,ATX,NBODY
 ,XTA,LOCSUBP
 ,AAX,M3L.
 ,ATX,LOCSUBP
 :10,VTM,-2
 ,XTA,STR+1
 CTDATA:15,ATX,
 ,AAX,M3L.
 10,ATX,NSET+2.
 ,STX,
 ,ASN,64+15
 10,VLM,CTDATA
 ,XTA,RELADR
 ,ATX,ASUBP.
 ,ARX,NBODY
 ,ATX,ADATA.
 ,ARX,NDATA
 ,ARX,NSET.
 ,ATX,DEFADR.
 ,ARX,C1.
 ,ATX,RELADR
 ,XTA,CDEFADR.
 ,ATX,WORDADR
 ,XTS,LOCSUBP
 14,VJM,WRITWP1
 ,XTA,4
 ,AAX,M4S.
 ,ATI,10
 14,VTM,ERSKIP.
 ,AEX,C1.
 ,U1A,ERROFIX
 ERSKIP:10,UTM,-1
 10,V1M,READFIXS
 ,XTA,STR
 ,ASN,64+12
 ,AAX,M4S.
 ,ATI,10
 PRODEF:10,VZM,FORMINST
 14,VJM,READWP1
 ,ATX,CARWORD
 10,UTM,-1
 ,AAX,MTYPE.
 ,ASN,64+18
 ,STX,TYPE
 ,WTC,TYPE
 ,UJ,SWITCH
 SWITCH:,UJ,PROCOMP
 :,UJ,PROLAB
 :,UJ,PROLOC
 :,UJ,PROSUBP
 :,UJ,PROS
 :,UJ,PROP
 :,UJ,PROP
 :,UJ,PROC
 PROCOMP:,XTA,CARWORD
 ,AAX,CIDENT.
 ,U1A,PROEQU
 ,XTA,CARWORD
 ,ASN,64+24
 PROEQU:,ATX,R.
 ,AAX,C4000.
 ,UZA,REDUSKIP
 ,XTA,R.
 ,AAX,C3777.
 ,ARX,CDEFADR.
 14,VJM,READWORD
 ,ATX,R.
 REDUSKIP:14,VJM,REDLOWA
 ,XTS,CARWORD
 ,AAX,CS.
 ,U1A,PROMUDIV
 ,STX,
 ,ARX,R.
 ,AAX,M3L.
 ,UJ,PROWEX
 PROMUDIV:,XTA,CARWORD
 ,AAX,CIDENT.
 ,U1A,PROWEQ
 ,XTA,CARWORD
 ,AAX,CMINUS.
 ,U1A,PRODIV
 ,XTA,R.
 ,AOX,CEXP.
 15,A*X,
 ,YTA,64-40
 ,AAX,M3L.
 ,UJ,PROWEX
 PRODIV:,STX,
 ,AOX,CEXP.
 ,NTR,2
 ,A+X,
 ,XTS,R.
 ,AOX,CEXP.
 ,A+X,
 15,A/X,
 ,NTR,3
 ,A+X,CEXP.
 ,AAX,M3L.
 ,UJ,PROWEX
 PROWEQ:15,XTA,-1
 ,A-X,ALF2
 ,UZA,ERWEQ
 ,STX,
 14,VJM,READWORD
 ,AAX,M3L.
 ,UJ,PROWEX
 ERWEQ:14,VJM,ERROEQ
 ,STX,
 ,XTA,
 ,UJ,PROWEX
 PROLAB:14,VJM,REDLOWA
 ,ARX,LOCSUBP
 ,AAX,M3L.
 PROWEX:14,VJM,WRITWP1
 ,UJ,PRODEF
 PROLOC:,XTS,CARWORD
 ,AAX,CS.
 ,U1A,PROPAR
 ,STX,
 ,UJ,PROSUBP
 PROPAR:,STX,
 14,VJM,REDLOWA
 ,AOX,CEXT.
 ,UJ,PROWEX
 PROSUBP:14,VJM,REDLOWA
 ,U1A,READFIXS
 ,STX,
 14,VJM,PUTNAME
 ,ATX,QNAME
 14,VJM,CHEKLIST
 ,XTA,ENTRY
 ,UJ,PROWEX
 PROS:,XTA,CARWORD
 ,AAX,CSP.
 ,U1A,ERRORSC
 ,XTS,AS
 14,VJM,REDLOWA
 ,ARX,AS
 ,STX,AS
 ,XTS,AC
 ,AEX,CAL7.
 ,ARX,AS
 ,UZA,SETS
 14,VJM,ERRORSC
 SETS:,STX,
 ,UJ,PROWEX
 PROP:14,VJM,REDLOWA
 ,XTS,CARWORD
 ,AAX,CSP.
 14,VTM,PROWEX
 ,UJ,INCRAP
 PROC:14,VJM,PUTNAME
 14,VJM,REDLOWA
 ,ASN,64-15
 ,STX,INFSUB
 ,ATX,QNAME
 ,AEX,/GLOBAS/.
 ,U1A,SKIPBAS
 ,XTA,INFSUB
 ,ASN,64-5
 ,ATX,GLOBASE
 ,UJ,PROWEX
 SKIPBAS:14,VJM,CHEKLIST
 ,XTA,ENTRY
 ,UJ,PROWEX
 PUTNAME:,XTS,CARWORD
 ,AAX,CLONGNAM.
 ,U1A,LONGNAME
 ,XTA,CARWORD
 ,AAX,UPPERMSK.
 14,UJ,
 LONGNAME:,XTA,CARWORD
 ,ASN,64+24
 ,AAX,C3777.
 ,ARX,DEFADR.
 ,UJ,READWORD
 REDLOWA:14,MTJ,9
 ,XTS,CARWORD
 ,AAX,M3L.
 ,XTS,CARWORD
 ,AAX,CDEF.
 ,UZA,CHMINUS
 ,STX,
 ,ARX,CDEFADR.
 14,VJM,READWORD
 ,XTS,
 CHMINUS:,XTA,CARWORD
 ,AAX,CMINUS.
 ,UZA,LOWEX
 ,XTA,CARWORD
 ,AAX,CS.
 ,U1A,LOWEX
 ,STX,
 ,AEX,M3L.
 ,ARX,C1.
 ,AAX,M3L.
 ,XTS,
 LOWEX:,STX,
 9,UJ,
 INCRAP:14,MTJ,9
 ,UZA,PROLP
 ,XTS,C1777.
 ,STX,CSMSK
 ,AAX,CS.
 ,UZA,IFNOTS
 ,XTA,C377.
 ,ATX,CSMSK
 IFNOTS:,XTA,ALF2
 ,AAX,CSMSK
 ,UZA,PROLP
 ,XTS,ALF2
 ,ARX,CSMSK
 ,ARX,C1.
 15,AEX,
 ,ATX,ALF2
 PROLP:,STX,
 ,ATX,AR.
 ,XTA,ALF2
 15,ATX,
 ,ARX,AR.
 ,ATX,ALF2
 ,XTA,AU.
 ,AEX,CAL7.
 ,ARX,ALF2
 ,UZA,SETP
 14,VJM,ERRORPU
 SETP:,STX,
 9,UJ,
 CHEKLIST:,ITA,14
 ,ATX,LISTRETN
 ,XTA,BEGLIST.
 ,ATX,LB.
 ,XTA,LISTEND
 ,ATX,LE.
 14,VJM,SLIST
 ,XTA,AQ
 ,UZA,COMPLECT
 5,XTA,1
 ,ATX,R.
 ,AAX,M3L.
 ,ATX,ENTRY
 5,XTA,1
 ,AAX,CFIX.
 ,U1A,READFIXS
 ,XTA,TYPE
 ,AEX,TCOMN.
 ,U1A,ELSKIP
 ,XTA,CARWORD
 ,AAX,CSP.
 ,UZA,CHLENGTH
 ,XTA,R.
 ,AAX,C377.
 ,U1A,ERLG
 ,XTA,CARWORD
 ,AAX,CS.
 ,U1A,CHLENGTH
 ,XTA,R.
 ,AAX,C1777.
 ,U1A,ERLG
 CHLENGTH:,XTA,R.
 ,ASN,64+15
 ,AAX,M3L.
 ,AEX,CAL7.
 ,XTS,INFSUB
 ,ASN,64+15
 ,AAX,M3L.
 15,ARX,
 ,UZA,ELSKIP
 ERLG:14,VJM,ERLENGTH
 ELSKIP:,XTA,R.
 EXLIST:,WTC,LISTRETN
 ,UJ,
 COMPLECT:,XTA,TYPE
 ,AEX,TCOMN.
 ,U1A,SUBP
 ,XTA,QNAME
 ,AAX,MSKSMB.
 ,AEX,CSTAR.
 ,U1A,COMMON
 SUBP:,XTA,ADRCAT.
 ,ATX,LB.
 ,XTA,CATEND
 ,ATX,LE.
 14,VJM,SLIST
 ,XTA,AQ
 ,U1A,COMPL
 ,XTA,TYPE
 ,AEX,TCOMN.
 ,UZA,COMMON
 14,VTM,EXLIST
 ,UJ,NAMEERRO
 COMPL:,ARX,C1.
 ,ATX,AQ
 5,XTS,1
 ,AAX,M3L.
 ,ATX,ENTRY
 5,UTM,1
 5,XTA,
 ,UJ,SKR
 SLIST:,ITS,14
 ,ITS,11
 ,XTS,
 ,ATX,AQ
 ,XTA,LB.
 SLEND:,A-X,LE.
 ,UZA,SEX
 15,ATX,
 ,ASN,64+1
 ,STI,11
 ,A+X,C1PAGE.
 ,UZA,ENDJP
 11,VTM,-512
 ENDJP:,XTA,LB.
 14,VJM,READWORD
 11,UTM,1
 ,AEX,QNAME
 LSTLP:,UZA,NORMEX
 5,XTA,2
 ,AEX,QNAME
 5,UTM,2
 11,VLM,LSTLP
 ,XTA,LB.
 ,A+X,C1PAGE.
 ,ATX,LB.
 ,UJ,SLEND
 NORMEX:,ITA,1
 ,ASN,64-10
 ,ITS,5
 ,AAX,C1777.
 15,AOX,
 ,ATX,AQ
 SEX:,STX,
 ,STI,11
 ,STI,14
 14,UJ,
 CMPLP:15,ATX,
 14,VJM,READWORD
 SKR:,AAX,INFMSK.
 ,U1A,STORINF
 ,STX,
 ,A-X,C2.
 ,ATX,AQ
 ,UJ,CMPLP
 STORINF:,XTA,TYPE
 ,AEX,TCOMN.
 ,UZA,BDATA
 STINF:5,XTA,
 STINF1:,AAX,FCLEAN.
 ,ATX,INFSUB
 ,ASN,64+15
 ,AAX,M3L.
 ,STX,R.
 ,UJ,STOLIST
 BDATA:5,XTA,
 ,ASN,64+15
 ,AAX,M3L.
 ,XTS,INFSUB
 ,ASN,64+15
 ,AAX,M3L.
 15,A-X,
 ,U1A,STINF
 ,ASN,64-15
 5,ARX,
 ,UJ,STINF1
 COMMON:,XTA,
 ,ATX,ENTRY
 ,XTA,INFSUB
 ,ASN,64+15
 ,ATX,R.
 ,UJ,STL1
 STOLIST:5,XTA,-1
 ,ATX,QNAME
 STL1:,XTA,R.
 ,XTS,CARWORD
 ,AAX,CSP.
 14,VJM,INCRAP
 ,ATX,R.
 ,A+X,ENTRY
 ,ATX,ENTRY
 ,XTA,INFSUB
 ,AAX,M3L.
 ,UZA,ZEROENTR
 ,XTA,INFSUB
 ,AAX,MLOCN.
 ,AOX,R.
 ,XTS,PB.
 14,VJM,WRITWEND
 ,XTA,INFSUB
 ,AAX,M3L.
 ,ATX,INFSUB
 ZEROENTR:,XTA,INFSUB
 ,ARX,R.
 ,XTS,QNAME
 SLP:14,VJM,WRITWEND
 ,XTA,INFSUB
 ,UZA,EXLIST
 ,XTA,AQ
 ,A+X,C2.
 ,ATX,AQ
 14,VJM,READWORD
 ,AAX,INFMSK.
 ,U1A,EXLIST
 5,XTA,
 ,ARX,R.
 5,XTS,-1
 ,UJ,SLP
 WRITWEND:,ITS,14
 ,ATX,WREX.
 ,XTA,LISTEND
 ,A-X,LISTMAX.
 ,U1A,ERWRSKIP
 14,VJM,ERROLIST
 ERWRSKIP:,XTA,LISTEND
 15,ATX,
 ,ARX,C2.
 ,STX,LISTEND
 14,VJM,WRITWORD
 5,ATX,1
 ,WTC,WREX.
 ,UJ,
 FORMINST:,XTA,MEMERFLG
 ,U1A,LOADQUE
 ,XTA,ERROFLAG
 ,U1A,LOADQUE
 ,XTA,STR
 ,ASN,64+24
 ,AAX,M4S.
 ,U1A,READFIXS
 ,XTA,ADR4
 ,ATI,10
 ,XTA,LOCSUBP
 ,ATX,WORDADR
 ,XTA,ASUBP.
 ,ATX,RELADR
 WORDLOOP:10,VZM,REWCON
 14,VJM,READWP1
 ,STX,CARWORD
 9,VTM,-1
 INSTLOOP:,XTA,CARWORD
 ,ASN,64+24
 ,ATX,INSTR
 ,AAX,CLADR.
 ,U1A,LINSTR
 ,XTA,INSTR
 ,AAX,CMADR.
 ,UZA,MDEF
 ,XTA,GLOBASE
 ,UZA,MDEF
 ,AAX,M4S.
 ,XTA,INSTR
 ,AAX,C3777.
 ,ARX,LOCSUBP
 ,ATX,R.
 ,UJ,MSKIP
 MDEF:,XTA,INSTR
 ,AAX,C4000.
 ,UZA,ENDINSTR
 ,XTA,INSTR
 ,AAX,C3777.
 ,ARX,CDEFADR.
 14,VJM,READWORD
 ,ARX,
 ,UZA,SETADR
 ,ATX,R.
 ,XTA,INSTR
 ,AAX,CMADR.
 ,UZA,MSKIP
 ,XTA,R.
 ,ARX,C70000.
 ,ATX,R.
 MSKIP:,XTA,INSTR
 ,AAX,MSINSTR.
 ,ATX,INSTR
 ,XTA,R.
 ,AAX,C10000.
 ,ASN,64-6
 ,AOX,INSTR
 ,XTS,R.
 ,AAX,M4S.
 15,AOX,
 ,ATX,INSTR
 ,XTA,R.
 ,ARX,C10000.
 ,AAX,C60000.
 ,UZA,ENDINSTR
 ,ATX,R.
 14,VTM,ENDINSTR
 ,XTA,GLOBASE
 ,UZA,ERROADR
 ,XTA,INSTR
 ,AAX,CMOD.
 ,U1A,ERROADR
 ,XTA,R.
 ,ASN,64-7
 ,ARX,GLOBASE
 ,ARX,CM1M.
 ,AOX,INSTR
 ,ATX,INSTR
 ENDINSTR:,XTA,CARWORD
 ,ASN,64-24
 ,AOX,INSTR
 ,ATX,CARWORD
 9,VLM,INSTLOOP
 ,XTS,CARWORD
 10,UTM,-1
 14,VJM,WRITWP1
 ,UJ,WORDLOOP
 CONVADR:,XTA,INSTR
 ,AAX,C3777.
 ,ARX,CDEFADR.
 14,VJM,READWORD
 ,UJ,SETADR
 LINSTR:,XTA,INSTR
 ,AAX,C40000.
 ,UZA,ENDINSTR
 ,XTA,INSTR
 ,AAX,C74000.
 ,AEX,C74000.
 ,UZA,CONVADR
 ,XTA,INSTR
 ,AAX,M37777.
 ,ARX,LOCSUBP
 SETADR:,AAX,M3L.
 ,XTS,INSTR
 ,AAX,MLINSTR.
 15,AOX,
 ,ATX,INSTR
 ,UJ,ENDINSTR
 REWCON:,XTA,RELADR
 ,AEX,ADATA.
 ,UZA,CHEKSET
 14,VJM,READWP1
 14,VTM,REWCON
 ,UJ,WRITWP1
 CHEKSET:,XTA,RELADR
 ,ARX,NDATA
 ,ATX,RELADR
 ,XTA,NSET.
 ,ATI,10
 ,XTA,LOCSUBP
 ,ARX,NBODY
 ,ARX,CM1.
 ,ATX,MENDATA
 ,AEX,CAL7.
 ,XTS,MENDATA
 ,ARX,NDATA
 ,AEX,CAL7.
 ,STX,MENDATA
 ,ATX,MENDSUB
 PUTSET:10,VZM,LOADQUE
 14,VJM,READWP1
 ,STX,CARWORD
 9,VTM,-3
 DATLP:,XTA,CARWORD
 ,AAX,C3777.
 9,ATX,ADR4
 ,XTA,CARWORD
 ,AAX,C4000.
 ,UZA,SKIPCONV
 9,XTA,ADR4
 ,ARX,CDEFADR.
 14,VJM,READWORD
 ,AAX,M3L.
 9,ATX,ADR4
 SKIPCONV:,XTA,CARWORD
 ,ASN,64+12
 ,ATX,CARWORD
 9,VLM,DATLP
 ,XTA,ADR2
 ,ATI,8
 SETLOOP:8,VZM,ESETLP
 ,XTA,ADR4
 ,XTS,ADR3
 ,STX,ADR
 ,ATI,9
 DATLOOP:9,VZM,EDATLP
 ,XTA,RETN
 ,U1A,CHAPD
 ,XTA,MODE
 ,A-X,ADR1
 ,UZA,ERD.
 CHAPD:,XTA,ADR1
 ,A-X,ALF2
 ,UZA,ERD
 ,XTA,ADR1
 ,A-X,AMIN.
 ,U1A,ERD
 ,XTA,ADR
 ,ATX,ADR2
 ,ARX,MENDATA
 ,UZA,SERSKIP
 ERD:14,VJM,ERROSET
 ,STX,
 ,UJ,SKWRDAT
 SERSKIP:,XTA,ADR
 ,ARX,MENDSUB
 ,UZA,DATSKIP
 ,ARX,CM1.
 ,ARX,ADATA.
 ,ATX,ADR2
 DATSKIP:,XTA,ADR2
 14,VJM,READWORD
 ,XTS,ADR1.
 14,VJM,WRITWORD
 SKWRDAT:,XTS,ADR1.
 ,ARX,C1.
 ,ATX,ADR1.
 ,XTA,ADR
 ,ARX,C1.
 ,ATX,ADR
 9,UTM,-1
 ,UJ,DATLOOP
 EDATLP:8,UTM,-1
 ,UJ,SETLOOP
 ESETLP:10,UTM,-1
 ,UJ,PUTSET
 NAMEERRO:6,VTM,4
 ,XTA,QNAME
 ,ATX,ABSFLG
 ,UJ,WRITER
 ERLENGTH:6,VTM,6
 ,XTA,QNAME
 ,UJ,WRITER
 ERRORSC:6,VTM,50B
 ,XTA,
 FATER:14,VTM,ENDLOAD
 ,UJ,WRITER
 ERRORPU:,XTA,MEMERFLG
 14,U1A,
 6,VTM,MODE
 ,XTA,C1.
 ,ATX,MEMERFLG
 ,XTA,AU.
 ,UJ,WRITADER
 ERROSET:6,VTM,CARWORD
 ,UJ,WRITER
 ERROFIX:6,VTM,61B
 ,UJ,FATER
 ERROEQ:6,VTM,ERROFLAG
 ,UJ,WRITER
 ERROADR:6,VTM,2
 ,XTA,WORDADR
 WRITADER:,ASN,64-33
 ,AUX,CUA.
 ,AOX, B 00000.
 WRITER:,ATX,ERN.
 ,ITA,14
 ,XTS,ERN.
 ,ITS,6
 ,AOX,ERROR
 ,ATX,ER.
 14,VJM,WRITWEND
 ,XTA,AQUEUE
 ,ARX,C2.
 ,ATX,AQUEUE
 LPWRER:,ATX,RER.
 ,ARX,CM2.
 ,A-X,LISTEND
 ,UZA,ENDWRER
 ,XTA,ER.
 ,XTS,ERN.
 ,XTS,RER.
 ,ARX,CM2.
 14,VJM,READWORD
 ,ATX,ER.
 5,XTA,1
 ,STX,ERN.
 5,STX,1
 5,ATX,
 ,XTA,RER.
 ,ARX,C2.
 ,UJ,LPWRER
 ENDWRER:,XTA,C1.
 ,ATX,ERROFLAG
 15,WTC,
 ,UJ,
 ERROLIST:,XTS,ERROR
 ,XTS,LISTEND
 ,ATX,ERROFLAG
 ,ARX,CM2.
 14,VJM,WRITWORD
 ,XTA,
 5,ATX,1
 ENDLOAD:,XTA,SAVE20
 ,CTX,OVFLJUMP
 ,XTA,C8.
 ,XTS,
 14,VJM,INCRAP
 ,ATX,ALF2
 ,CTX,1
 ,ITA,
 11,VTM,-2
 PACKDIST:,ASN,64-15
 11,XTS,AU.
 ,AAX,M3L.
 15,AOX,
 11,VLM,PACKDIST
 ,ATX,ALF1
 ,XTA,LISTEND
 ,A-X,BEGLIST.
 ,ASN,64-30
 ,AOX,ALF2
 ,ATX,ALF2
 ,XTA,ERROFLAG
 ,UZA,CHEKLIBS
 ,XTA,ABSFLG
 ,UZA,SKIPPR
 ,XTA,LTAPES
 ,UZA,SKIPPR
 ,XTA,TYESFLG.
 ,UZA,DRUMOFF
 SKIPPR:,XTA,
 ,ATX,TYESFLG.
 ,ATX,ABSFLG
 ,XTA,LISTEND
 ,A-X,SECLIST.
 ,AOX,INT0.=I0
 ,A/X,REAL6*1.=R6.1
 ,A+X,INT0.=I0
 ,AAX,M3L.
 ,ARX,C2.
 ,ATI,7
 ,ASN,64-1
 ,ATX,DPRINT
 14,VJM,PRINT6
 LP1:,XTA,SECLIST.
 ,ATX,WORDADR
 8,VTM,-3
 9,VTM,BUF.
 LP2:,XTA,WORDADR
 ,A-X,LISTEND
 ,U1A,LP20
 :,AOX,
 ,U1A,LP2E
 ,XTA,FRIE.
 5,VTM,ALF1
 ,UJ,LP2F
 LP20:,XTA,WORDADR
 14,VJM,READWORD
 ,AAX,MSKER.
 ,AEX,ERROR
 ,U1A,LP2F1
 SETERROR:5,XTA,
 ,AAX,M37.
 ,ATI,14
 14,UTM,ERTEXTS.
 14,XTA,
 9,ATX,
 14,XTA,1
 9,ATX,1
 14,XTA,2
 9,ATX,2
 5,XTA,
 ,AAX,M40.
 ,U1A,LP2E
 5,XTA,1
 9,ATX,2
 ,UJ,LP2E
 LP2F1:5,XTA,
 LP2F:9,ATX,1
 5,XTA,1
 ,ASN,64-33
 ,AUX,CUA.
 ,AOX,C50.
 9,ATX,2
 5,XTA,1
 ,AAX,LIBMASK.
 ,U1A,LP2E
 9,XTA,2
 ,AOX,CC.
 9,ATX,2
 5,XTA,1
 ,AAX,INFMSK.
 ,U1A,LP2E
 ,XTA,CTE.
 9,ARX,2
 9,ATX,2
 LP2E:9,UTM,3
 ,XTA,WORDADR
 ,ARX,DPRINT
 ,ATX,WORDADR
 8,VLM,LP2
 14,VJM,PRINT6
 ,XTA,SECLIST.
 ,ARX,C2.
 ,ATX,SECLIST.
 7,UTM,-1
 7,V1M,LP1
 14,VTM,DRUMOFF
 PRINT6:,ITA,14
 ,ITS,
 1,VTM,-14
 11,VTM,BUF.
 10,VTM,BUFP.
 4,VTM,-5
 RW:2,VTM,-7
 RS:11,XTA,
 ,ASN,64-6
 11,ATX,
 ,YTA,
 5,VTM,-1
 SCHA:,ATI,14
 ,A-X,C6.
 5,UTM,1
 ,UZA,SCHA
 5,XTA,TABL
 14,UTM,1
 SCHS:,ASN,64-8
 14,UTM,-1
 14,V1M,SCHS
 ,YTA,
 10,XTS,
 ,ASN,64-8
 15,AOX,
 10,ATX,
 4,VLM,ENDRS
 10,UTM,1
 4,VTM,-5
 ENDRS:2,VLM,RS
 11,UTM,1
 1,VLM,RW
 ,XTA,CSTR.
 10,ATX,
c опциональный уход на процедуру печати
с по адресу, указанному в SHIFR
 :,WTC,SHIFR
 14,VTM,
 14,VZM,standpr
 8,VTM,BUFP.
 ,ITS,13
 ,ITS,8
 ,ITS,10
 ,XTS,
 :14,UTC,
 13,VJM,
 ,STI,13
 ,UJ,afterpr
 standpr:,PRINT,INF6
 afterpr:,XTA,
 14,VTM,-14
 :11,UTM,-1
 11,ATX,
 14,VLM,*-1.
 15,WTC,.
 ,UJ,
 INF6:10,Z00,-20
 10,Z00,-20
 ,,
 8,,
 CHEKLIBS:,XTA,ALFA.
 ,ARX,
 ,U1A,SKIPPR
 DRUMOFF:,XTA,NLIST.
 ,ATI,1
 ,A-X,NCAT.
 ,ATI,10
 WRLIST:1,XTA,TCOR
 ,AAX,CHM.
 ,UZA,WSK
 1,XTA,TCOR
 ,AAX,CMEM.
 ,ITS,1
 ,ASN,64-34
 15,ARX,
 ,AAX,CMEM.
 ,ATX,FRIEPAGE
 14,VJM,SETFDR
 14,VJM,WRITDRUM
 WSK:1,UTM,1
 10,UTM,1
 10,V1M,WRLIST
 ,WTC,NPRT
 1,VTM,
 RDMEM:1,XTA,TCOR
 ,AAX,CHM.
 ,U1A,RSK
 ,ITA,1
 ,ASN,64-34
 ,ATX,FRIEPAGE
 14,VJM,SETFDR
 ,XTA,FRIEDR
 ,U1A,RDM
 ,ITA,1
 ,ASN,64-10
 ,A-X,C8.
 ,XTS,ALF2
 ,AAX,M3L.
 15,X-A,
 ,UZA,RSK
 14,VTM,RSK
 ,UJ,ZEROS1
 RDM:14,VJM,READDRUM
 RSK:1,UTM,-1
 1,V1M,RDMEM
 EXIT:1,VTM,-2
 STORETN:1,UTC,2
 ,XTA,CRETN
 1,CTX,RETN+2
 1,VLM,STORETN
 ,XTA,MODE
 ,UZA,SKM1
 ,ATI,14
 ,XTA,ALF1
 14,ATX,5
 ,XTA,ALF2
 14,ATX,6
 ,XTA,SECENTRY
 14,ATX,7
 14,XTA,
 ,AEX,75B
 ,U1A,nomatch
 ,WTC,SECENTRY
 ,ATX,
 nomatch:,XTA,SRETN
 ,CTX,RETN+2
 SKM1:,XTA,ERROFLAG
 ,UZA,SKM2
 ,XTA,ABSFLG
 ,U1A,SRCHLIBS
 ,XTA,ERETN
 ,CTX,RETN+2
 SKM2:14,VTM,-12
 RESTIRS:14,XTA,IR13
 14,ATI,13
 14,VLM,RESTIRS
 ,UJ,RETN
 SRCHLIBS:,DRUM,WPAGE1.
 ,DRUM,LOADLIBR
 ,UJ,2000B
 CRETN:,DRUM,RLEX.
 ,STX,
 ,XTR,REGIM
 ,WTC,SECENTRY
 ,UJ,
 SRETN:13,UJ,
 ERETN:,UJ,OVFLJUMP
 :,BSS,3
 FRIMEMJ:,UJ,FRIMEM
 ,,
 CGIVPLAC:,ATI,5
 ,ASN,64+10
 CGIVPLC1:,ATI,1
 1,UJ,TCOR
 NORMJUMP:5,UTM,
 6,UJ,
 PRTJUMP:5,UTM,
 ,UJ,SETPRT
 EXTJUMP:,UJ,CHEKPAGE
 ,,
 ERJUMP:,MTJ,13
 ,UJ,READFIXS
 C1:,LOG,1
 M3L:,LOG,7 7777
 CM1:,LOG,7777 7777 7777 7776
 LIBMASK:,OCT,5777 77
 M4S:,LOG,7777
 MTYPE:,LOG,700 0000
 C1777:,LOG,1777
 C4000:,LOG,4000
 C1PAGE:,LOG,2000
 CAL7:,LOG,7777 7777 7777 7777
 C3777:,LOG,3777
 CDEF:,LOG,10 0000
 CMINUS:,LOG,20 0000
 CLONGNAM:,LOG,2000 0000
 UPPERMSK:,OCT,7777 7777
 CFIX:,OCT,2
 CMADR:,LOG,100 0000
 C70000:,LOG,7 0000
 CLADR:,LOG,200 0000
 MSINSTR:,LOG,7677 0000
 C40000:,LOG,4 0000
 C74000:,LOG,7 4000
 M37777:,LOG,3 7777
 MLINSTR:,LOG,7770 0000
 CIDENT:,LOG,4000 0000
 MLOCN:,OCT,7777 7777 777
 M37:,LOG,37
 TSUBP:,LOG,3
 C2:,LOG,2
 CM1M:,LOG,7777 7777 7377 7777
  B 00000:,TEXT,8H B 00000
 TCOMN:,LOG,7
 PB:,LOG,6042
 CEXT:,OCT,4
 C377:,LOG,377
 CSP:,LOG,1040 0000
 CS:,LOG,1000 0000
 CEXP:,INT,0
 MSKSMB:,OCT,77
 CSTAR:,OCT,12
 INFMSK:,OCT,5777 7777 777
 MPACKSA:,OCT,4037 77
 MSEG:,OCT,174
 CREAD:,OCT,001
 CMEM:,OCT,0007 7777
 MEMCLEAN:,LOG,7770 0000 7777 7777
 MPDRUM:,LOG,4 0000 0077 0037
 CHM:,LOG,2000 0000
 LOWMSK:,LOG,7777 7777
 CPRT:,LOG,1000 0000
 CUNPDRUM:,LOG,4 0000 0077 0037
 BTAPE:,LOG,1400
 CTAPE:,LOG,3000
 CUNPTAPE:,LOG,4 0000 0077 0777
 C33777:,LOG,3 3777
 FCLEAN:,LOG,5777 7777 7777 7777
 CDEFADR:,LOG,10 0000
 NDEFPLAC:,LOG,40
 BEGLIST:,LOG,10 4000
 NLIST:,LOG,42
 LISTMAX:,LOG,11 0000
 ADRCAT:,LOG,11 0000
 NCAT:,LOG,44
 LIBADR:,LOG,11 4000
 NLIBR:,LOG,46
 NLIBRMAX:,LOG,110
 LSTDRUM:,LOG,1000
 AMIN:,LOG,100
 C8:,LOG,10
 CNFRIEDR:,LOG,12
 RPAGE1:,LOG,0010 0100 0020 0026
 WPAGE1:,LOG,0000 0100 0020 0026
 ERROR:,TEXT,8H ERROR
 /GLOBAS/:,TEXT,8H1970
 CM2:,LOG,7777 7777 7777 7775
 C10000:,LOG,1 0000
 C60000:,LOG,6 0000
 CMOD:,LOG,7400 0000
 CUA:,LOG,7 0707 0707
 C50:,LOG,20 2020 2020
 MP:,LOG,7 6000
 TABL:,GOST,6H ′133′БЦДФ
 ,GOST,24HГИ()*ЙЛЯЖ/0123456789Ь;П-
 ,GOST,24H+ЫЗABCDEFGHIJKLMNOPQRSTU
 ,GOST,12HVWXYZШЭЩЧЮ /
 CSTR:,OCT,462
 INT0:,INT,0
 REAL6*1:,REAL,6.1
 MSKER:,OCT,7777 7777 7777 7700
 CC:,TEXT,2H C
 CTE:,OCT,0002
 M40:,LOG,40
 C6:,LOG,6
 ERTEXTS:,BSS,
 ,TEXT,8H ДЛИHHЫЙ
 ,TEXT,8H CПИCOK
 ,TEXT,8H   ДЛИHH
 ,TEXT,8HЫЙ AДPEC
 ,TEXT,8H   OTCYT
 ,TEXT,8HCTBYET
 ,TEXT,8H HEBEPHA
 ,TEXT,8HЯ ДЛИHA
 ,TEXT,8H ПEPEПOЛ
 ,TEXT,8HHEHИE S-
 ,TEXT,8HOБЛACTИ
 ,TEXT,8H     MAЛ
 ,TEXT,8HO ПAMЯTИ
 ,TEXT,8H
 ,TEXT,8H OШИБKA
 ,TEXT,8HB PACCЫЛ
 ,TEXT,8HKE DATA
 ,TEXT,8H ИCПOPЧE
 ,TEXT,8HHA ПOДПP
 ,TEXT,8HOГPAMMA
 ,TEXT,8H ПЛOXAЯ
 ,TEXT,8HЭKBИBAЛE
 ,TEXT,8HHTHOCTЬ
 FRIE:,TEXT,8H  CBOБOД
 ,END,
C To compare:
C besmtool dump 2048 --start=0207 --length=1 > orig.dump
C besmtool dump 80 --start=0207 --length=1 > new.dump
C diff orig.dump new.dump
 DUMPER:,NAME,
 END:,LP,0
 SPACER:,LP,2000B-END
 REDUOL:,SUBP,
 ,*70, WRITE
 ,SJ,
 WRITE:,LOG,0000 0100 0067 0207
 ,END,
*main dumper
*execute
*end file
``````
еконец
